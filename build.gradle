buildscript {
    ext.isCiBuild = System.getenv("CI") == "true"

    ext.kotlinVersion = '1.3.72'
    ext.detektVersion = '1.9.1'
    ext.pluginPublishingPluginVersion = '0.11.0'
    ext.mavenPublishPluginVersion = '0.11.1'
    ext.androidGradlePluginVersion = '4.1.0-beta01'
    ext.junitVersion = '4.13'
    ext.truthVersion = '1.0.1'
}

plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version "$pluginPublishingPluginVersion"
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
    id 'io.gitlab.arturbosch.detekt' version "$detektVersion"
    id 'com.vanniktech.maven.publish' version "$mavenPublishPluginVersion"
}

repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
}

group = GROUP
version = VERSION_NAME

pluginBundle {
    website = POM_URL
    vcsUrl = POM_SCM_URL
    tags = ['gradle', 'android', 'firebase-test-lab', 'testing']
    mavenCoordinates {
        groupId = GROUP
        artifactId = POM_ARTIFACT_ID
    }
}

gradlePlugin {
    plugins {
        firestorm {
            id = 'io.github.reactivecircus.firestorm'
            displayName = POM_NAME
            description = POM_DESCRIPTION
            implementationClass = 'reactivecircus.firestorm.FirestormPlugin'
        }
    }
}

configurations {
    fixtureClasspath
}

tasks.getByName('pluginUnderTestMetadata')
        .getPluginClasspath()
        .from(configurations.fixtureClasspath)

sourceSets {
    functionalTest {}
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

dependencies {
    implementation platform("org.jetbrains.kotlin:kotlin-bom:$kotlinVersion")
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compileOnly "com.android.tools.build:gradle:$androidGradlePluginVersion"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "com.google.truth:truth:$truthVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    testImplementation "com.android.tools.build:gradle:$androidGradlePluginVersion"

    fixtureClasspath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    fixtureClasspath "com.android.tools.build:gradle:$androidGradlePluginVersion"
}

task functionalTest(type: Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}


check {
    dependsOn(tasks.functionalTest)
}

tasks.withType(Test).configureEach {
    maxParallelForks Runtime.runtime.availableProcessors() * 2
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detektVersion"
}

detekt {
    input = files("src/")
    failFast = true
    config = files("${rootDir}/detekt.yml")
    buildUponDefaultConfig = true
    reports {
        html.destination = file("${project.buildDir}/reports/detekt/${project.name}.html")
    }
}
